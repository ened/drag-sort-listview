buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.+'
    }
}

apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = 'drag-sort-listview'
group = 'com.github.ened'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:18.0.+'
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.0.1"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}

// TODO: Add a task that prints the test results.
task instrumentTest(dependsOn: connectedCheck)

task apklib(type: Zip) {
    dependsOn 'packageReleaseJar'
    appendix = extension = 'apklib'

    from 'AndroidManifest.xml'
    into('res') {
        from 'res'
    }
    into('src') {
        from 'src'
    }
}

artifacts {
    archives apklib
}

signing {
    sign configurations.archives
}

if (!hasProperty("sonatypeUsername")) {
    sonatypeUsername = ""
}
if (!hasProperty("sonatypePassword")) {
    sonatypePassword = ""
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            modifyPom(addFilter('aar') { artifact, file ->
                artifact.name == 'drag-sort-listview'
            })

            modifyPom(addFilter('apklib') { artifact, file ->
                artifact.name == 'drag-sort-listview-apklib'
            })

            // There's no official apklib for Google Play services, so we
            // can't really maintain that dependency for the apklib packaging.
            pom('apklib').whenConfigured { pom ->
                pom.dependencies = []
            }
        }
    }
}

def modifyPom(pom) {
    pom.project {
        name 'DragSortListView'
        description 'Android ListView with drag and drop reordering.'
        url 'https://github.com/ened/drag-sort-listview'

        scm {
            url 'https://github.com/ened/drag-sort-listview.git'
            connection 'git@github.com:ened/drag-sort-listview.git'
            developerConnection 'git@github.com:ened/drag-sort-listview.git'
        }
			}
}
